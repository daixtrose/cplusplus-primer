# language: cmake 

include(GNUInstallDirs)

# Adding RUNTIME_DEPENDENCIES and installing in private directory
install(TARGETS ${PROJECT_NAME} RUNTIME_DEPENDENCIES)

# Debug CMake
include(CMakePrintHelpers)
cmake_print_variables(
    PROJECT_VERSION_MAJOR 
    PROJECT_VERSION_MINOR 
    PROJECT_VERSION_PATCH)

# These are project-specific
set(CPACK_PACKAGE_NAME ${PROJECT_NAME})
set(CPACK_PACKAGE_VENDOR "Daixtrose Engineering")
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "An example project")
set(CPACK_PACKAGE_CONTACT "daixtrose.engineering@gmx.de")
set(CPACK_DEBIAN_PACKAGE_MAINTAINER "Markus Werle")
set(CPACK_PACKAGE_INSTALL_DIRECTORY ${CPACK_PACKAGE_NAME})
set(CPACK_PACKAGE_VERSION_MAJOR ${PROJECT_VERSION_MAJOR})
set(CPACK_PACKAGE_VERSION_MINOR ${PROJECT_VERSION_MINOR})
set(CPACK_PACKAGE_VERSION_PATCH ${PROJECT_VERSION_PATCH})
set(CPACK_VERBATIM_VARIABLES YES)
set(CPACK_PACKAGE_DESCRIPTION_FILE ${CMAKE_CURRENT_LIST_DIR}/Description.txt)
set(CPACK_RESOURCE_FILE_WELCOME ${CMAKE_CURRENT_LIST_DIR}/Welcome.txt)
set(CPACK_RESOURCE_FILE_LICENSE ${CMAKE_CURRENT_LIST_DIR}/License.txt)
set(CPACK_RESOURCE_FILE_README ${CMAKE_CURRENT_LIST_DIR}/UIInstallerReadme.txt)

# Configure what kind of packages will be created based on the platform
if(WIN32)
    set(CPACK_GENERATOR ZIP WIX)
elseif(APPLE)
    set(CPACK_GENERATOR TGZ productbuild)

    # TODO: find out what can be done on Apple platform
elseif(CMAKE_SYSTEM_NAME STREQUAL "Linux")
    set(CPACK_GENERATOR TGZ RPM DEB)
    set(CPACK_DEBIAN_PACKAGE_SHLIBDEPS "TRUE")

    if(CMAKE_SOURCE_DIR STREQUAL CMAKE_CURRENT_SOURCE_DIR)
        # Set specific install directory 
        set(CMAKE_INSTALL_PREFIX "/opt/daixtrose/${PROJECT_NAME}-${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}")
        cmake_print_variables(CMAKE_INSTALL_PREFIX)
        set(CPACK_PACKAGING_INSTALL_PREFIX ${CMAKE_INSTALL_PREFIX})
        
        file(RELATIVE_PATH libDir
            ${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_INSTALL_BINDIR}
            ${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR}
            )
        
        set_target_properties(${PROJECT_NAME} 
            PROPERTIES
            BUILD_RPATH_USE_ORIGIN ON
            INSTALL_RPATH "$ORIGIN:$ORIGIN/${libDir}"
        )
    endif()
else()
    set(CPACK_GENERATOR TGZ)
endif()

# This is what writes out the input file for CPack
include(CPack)
